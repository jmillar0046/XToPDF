plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.xtopdf'

// Dynamic versioning based on git tags
version = providers.exec {
	commandLine('git', 'describe', '--tags', '--always', '--dirty')
}.standardOutput.asText.getOrElse('0.0.1-SNAPSHOT').trim().replace('v', '')

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

configurations.configureEach {
	resolutionStrategy {
		force 'org.apache.commons:commons-compress:1.28.0'
		force 'org.apache.commons:commons-lang3:3.18.0'
		force 'commons-beanutils:commons-beanutils:1.9.4'
		force 'org.apache.jena:jena-core:5.5.0'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.apache.poi:poi:5.3.0'
  implementation 'org.apache.poi:poi-ooxml:5.3.0'
  implementation 'org.apache.poi:poi-scratchpad:5.3.0'
	implementation 'com.itextpdf:itext-core:8.0.5'
	implementation 'com.itextpdf:layout:8.0.5'
	implementation 'com.itextpdf:pdfa:8.0.5'
  implementation 'com.itextpdf:html2pdf:5.0.5'
  implementation 'com.itextpdf:svg:8.0.5'
	implementation 'org.apache.pdfbox:pdfbox:3.0.3'
  implementation 'com.github.jai-imageio:jai-imageio-core:1.4.0'
  implementation 'org.commonmark:commonmark:0.24.0'
  implementation('org.odftoolkit:odfdom-java:0.12.0') {
    exclude group: 'org.slf4j', module: 'slf4j-simple'
  }
  
  // Security vulnerability fixes - use latest stable versions
  implementation 'org.apache.commons:commons-lang3:3.18.0'
  implementation 'commons-beanutils:commons-beanutils:1.9.4'
  implementation 'org.apache.jena:jena-core:5.5.0'
  
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
}

task generateTestFiles(type: JavaExec) {
    group = 'verification'
    description = 'Generate binary test files for testing'
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.xtopdf.xtopdf.TestFileGenerator'
}
