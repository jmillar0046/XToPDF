plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.xtopdf'

// Dynamic versioning based on git tags
version = providers.exec {
	commandLine('git', 'describe', '--tags', '--always', '--dirty')
}.standardOutput.asText.getOrElse('0.0.1-SNAPSHOT').trim().replace('v', '')

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

configurations.configureEach {
	resolutionStrategy {
		force 'org.apache.commons:commons-compress:1.28.0'
		force 'org.apache.commons:commons-lang3:3.20.0'
		force 'commons-beanutils:commons-beanutils:1.11.0'
		force 'org.apache.jena:jena-core:5.6.0'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:3.5.7'
	implementation 'org.apache.poi:poi:5.4.1'
  implementation 'org.apache.poi:poi-ooxml:5.4.1'
  implementation 'org.apache.poi:poi-scratchpad:5.4.1'
	implementation 'org.apache.pdfbox:pdfbox:3.0.6'
	implementation 'org.jsoup:jsoup:1.21.2'
  implementation 'com.github.jai-imageio:jai-imageio-core:1.4.0'
  implementation 'org.commonmark:commonmark:0.24.0'
  implementation('org.odftoolkit:odfdom-java:0.12.0') {
    exclude group: 'org.slf4j', module: 'slf4j-simple'
  }
  
  // Security vulnerability fixes
  implementation 'org.apache.commons:commons-lang3:3.20.0'
  implementation 'commons-beanutils:commons-beanutils:1.11.0'
  implementation 'org.apache.jena:jena-core:5.6.0'
  
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.5.7'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
}
